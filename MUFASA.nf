#!/usr/bin/env nextflow

// ================= HELP MESSAGE ==================
if ( params.help ) {
    log.info """
    MFA / MuFASA — Multi-Functional Annotation pipeline
    ===================================================

    MFA integrates two independent annotation modules:
      1) Homology-based annotation (DIAMOND + AHRD)
      2) Embedding-based annotation (FANTASIA Lite)

    Each module requires external resources whose paths MUST be provided.

    --------------------------------------------------
    USAGE
    --------------------------------------------------
    nextflow run main.nf \\
        --input  <input.csv> \\
        --outdir <results/>

    --------------------------------------------------
    CORE PARAMETERS (REQUIRED)
    --------------------------------------------------
    --input
        Input CSV file describing the samples/proteomes to annotate

    --outdir
        Output directory

    --------------------------------------------------
    MODULE 1: HOMOLOGY-BASED ANNOTATION (DIAMOND + AHRD)
    --------------------------------------------------
    This module performs sequence similarity searches using DIAMOND
    and functional annotation refinement using AHRD.

    Required parameters:

    --dbsprot
        Path to DIAMOND SwissProt database (.dmnd)

    --dbtrembl
        Path to DIAMOND TrEMBL database (.dmnd)

    --GO_GAF
        Gene Ontology annotation file (GOA, .gaf)

    --UNIPROT_SPROT
        SwissProt protein FASTA file

    --UNIPROT_TREMBL
        TrEMBL protein FASTA file

    --BLACKLIST
        AHRD description blacklist file

    --FILTER_SPROT
        AHRD SwissProt filter file

    --FILTER_TREMBL
        AHRD TrEMBL filter file

    --TOKEN_BLACKLIST
        AHRD token blacklist file

    --AHRD_JAR
        Path to the AHRD JAR file

    Optional parameters:

    --threads
        Number of threads for DIAMOND
        (default: 60)

    --evalue
        E-value threshold for DIAMOND
        (default: 1e-20)

    --max_target_seqs
        Maximum number of hits per query
        (default: 1)

    --JAVA_XMX
        Maximum Java heap size for AHRD
        (default: 2g)

    --------------------------------------------------
    MODULE 2: EMBEDDING-BASED ANNOTATION (FANTASIA LITE)
    --------------------------------------------------
    This module performs functional annotation using protein embeddings
    generated by FANTASIA Lite.

    Optional parameters:

    --fantasia_models
        List of embedding models to use
        (default: prot_t5)

    --------------------------------------------------
    EXAMPLE
    --------------------------------------------------
    nextflow run main.nf \\
        --input input.csv \\
        --outdir results \\
        --dbsprot /path/to/sprot.dmnd \\
        --dbtrembl /path/to/trembl.dmnd \\
        --GO_GAF /path/to/goa.gaf \\
        --UNIPROT_SPROT /path/to/sprot.fasta \\
        --UNIPROT_TREMBL /path/to/trembl.fasta \\
        --AHRD_JAR /path/to/ahrd.jar

    --------------------------------------------------
    NOTES
    --------------------------------------------------
    • All paths to external resources are mandatory for the
      corresponding module to run.
    • Modules can be enabled/disabled in the workflow logic.
    • Use -resume to continue a previous execution.
    """
    exit 0
}


// ================== DEFAULT CONFIGURATION FOR MFA PIPELINE ==================
// Define parameters
params.input = "./test/test.csv"
params.outdir = "results"

// FANTASIA parameters
params.fantasia_models = "prot_t5"

// DIAMOND database paths
params.dbsprot = "/data/shared_dbs/swissprot/uniprot_sprot_r2025_01.dmnd"
params.dbtrembl = "/data/shared_dbs/swissprot/uniprot_trembl_r2025_01.dmnd"
// DIAMOND parameters
params.threads = 60
params.evalue = 1e-20
params.max_target_seqs = 1

// AHRD parameters
params.GO_GAF = "/data/shared_dbs/swissprot/goa_uniprot_all.gaf"
params.UNIPROT_SPROT = "/data/shared_dbs/swissprot/uniprot_sprot_r2025_01.fasta"
params.UNIPROT_TREMBL = "/data/shared_dbs/swissprot/uniprot_trembl_r2025_01.fasta"
params.BLACKLIST = "/data/software/AHRD/test/resources/blacklist_descline.txt"
params.FILTER_SPROT = "/data/software/AHRD/test/resources/filter_descline_sprot.txt"
params.FILTER_TREMBL = "/data/software/AHRD/test/resources/filter_descline_trembl.txt"
params.TOKEN_BLACKLIST = "/data/software/AHRD/test/resources/blacklist_token.txt"
params.AHRD_JAR = "/data/software/AHRD/dist/ahrd.jar"
params.JAVA_XMX = "2g"   // sube a "8g" o más si lo necesitas


// ================= MODULES ==================
include { run_fantasia } from './modules/run_FANTASIA.nf'
include { run_diamond } from './modules/run_HOMOLOGY.nf'
include { write_yaml } from './modules/run_HOMOLOGY.nf'
include { run_AHRD } from './modules/run_HOMOLOGY.nf'


// ================= WORKFLOW DEFINITION ==================
workflow {

    ch_samples = Channel.fromPath(params.input)
                        .splitCsv(header: true)
                        .map { row -> tuple(row.species, file(row.fasta)) }

    run_fantasia(ch_samples)

    ch_dbs = Channel.of(params.dbsprot, params.dbtrembl)
    ch_diamond = ch_samples.combine(ch_dbs)
    ch_diamond_out = run_diamond(ch_diamond)
    grouped_ch = ch_diamond_out
                .groupTuple(by: 0)
                .map { species, fasta_list, hit_list ->
                        def fasta = fasta_list[0]
                        def trembl_tsv = hit_list.find { it.getName().contains('trembl') }
                        def sprot_tsv = hit_list.find { it.getName().contains('sprot') }
                        tuple(
                            species,
                            fasta,
                            trembl_tsv,
                            sprot_tsv
                        )
                }
    ch_yaml = write_yaml(grouped_ch)
    run_AHRD(ch_yaml)
}
